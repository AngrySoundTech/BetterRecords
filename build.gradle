/////////////////////////////
// BuildScript and Plugins //
/////////////////////////////

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:latest.release"
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.3'
    }
}

subprojects {
    
    /////////////////////////////
    // BuildScript and Plugins //
    /////////////////////////////
    
    buildscript {
        repositories {
            jcenter()
        }
    }

    apply plugin: 'java'
    apply plugin: 'com.jfrog.artifactory'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.johnrengelman.shadow'

    //////////////////
    // Dependencies //
    //////////////////
    
    repositories {
        jcenter()
    }
    
    ///////////////
    // Artifacts //
    ///////////////

    shadowJar {
        classifier '' // Replace the default jar

        dependencies {
            exclude dependency('junit:junit')
        }
    }

    ////////////////
    // Publishing //
    ////////////////

    publishing {
        publications {
            maven(MavenPublication) {
                for (theArtifact in configurations.archives.allArtifacts) {
                    artifact theArtifact
                }
            }
        }
    }
}

////////////////
// Publishing //
////////////////

artifactoryPublish.skip = true

artifactory {
    contextUrl = "https://artifactory.feldman.tech/artifactory/"
    publish {
        repository {
            repoKey = System.getenv('CIRCLE_BRANCH') ==~ /^dev$/ ? 'minecraft-snapshots' :
                      System.getenv('CIRCLE_BRANCH') ==~ /^master$/ ? 'minecraft-releases' : null
            username = System.getenv('ARTIFACTORY_USER')
            password = System.getenv('ARTIFACTORY_PASSWORD')
        }
        defaults {
            publications('maven')
            publishArtifacts = true
        }
    }
}
