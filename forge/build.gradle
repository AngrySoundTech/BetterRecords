buildscript {
    repositories {
        maven {
            name 'forge'
            url 'http://files.minecraftforge.net/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version "1.3.11"
}

apply plugin: 'net.minecraftforge.gradle.forge'

repositories {
    maven {
        url 'http://maven.shadowfacts.net/'
    }
}

dependencies {
    compile project(path: ':BetterRecords-common', configuration: 'shadow')

    shadow group: 'net.shadowfacts', name: 'Forgelin', 'version': forgelin_version
}

version = "$minecraft_version-$mod_version"

minecraft {
    version = "$minecraft_version-$forge_version"
    mappings = "$forge_mappings"
    runDir = "run"

    replaceIn "Constants.kt"
    replace "@VERSION@", project.version
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

shadowJar {
    exclude 'dummyThing'
}

reobf {
    shadowJar {}
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives deobfJar
    archives sourcesJar
    archives javadocJar
}
